buildscript {
    ext.kotlin_version = "1.3.70"
    ext.toml_version = "0.2.128.3278-202"
    ext.tuweni_toml_version = "1.1.0"
    ext.jvm_version = "11"
    ext.language_version = "1.3"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version", "org.apache.tuweni:tuweni-toml:$tuweni_toml_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.5.0"
}

intellij {
    pluginName project.name
    version "2020.2.2"
    type "PC"
    updateSinceUntilBuild false
    downloadSources true
    plugins "python-ce", "org.toml.lang:$toml_version"
}

patchPluginXml {
    sinceBuild "202.7319.50"
    untilBuild "203.*"
}

allprojects {
    apply plugin: "org.jetbrains.intellij"
    apply plugin: "kotlin"
    apply plugin: "jacoco"
    repositories {
        mavenCentral()
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = jvm_version
            languageVersion = language_version
            apiVersion = language_version
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = jvm_version
            languageVersion = language_version
            apiVersion = language_version
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.13.1'
        implementation "org.apache.tuweni:tuweni-toml:$tuweni_toml_version"
        compile "org.jetbrains:annotations:20.0.0"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }

    sourceCompatibility = jvm_version
    targetCompatibility = jvm_version

}

sourceSets {
    main {
        java.srcDir 'src'
        resources.srcDir 'resources'
    }
    test {
        java.srcDir 'testSrc'
        resources.srcDir 'testData'
    }
}
